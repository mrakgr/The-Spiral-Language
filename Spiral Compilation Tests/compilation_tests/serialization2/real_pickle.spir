open real_core

inl size_prim forall t. =
    typecase t with
    | i32 => 4
    | char => 2
inl rec size x : i32 =
    match x with
    | () => 0
    | a,b => size a + size b
    | {} => record_fold (fun {state key value} => state + size value) 0 x
    | _ when symbol_is x => 0
    | _ => 
        typecase `x with
        | a _ ~t =>
            if prim_type_is `t then size_prim `i32 + size_prim `t * length `a `i32 `t x
            else am.fold `a `i32 `i32 `t (fun s x => s + size x) (size_prim `i32) x
        | string => size_prim `i32 + size_prim `char * sm.length `i32 x
        | ~t => 
            if prim_is x then size_prim `t
            // Assume it is an union type.
            else inl ~x = x in (join size_prim `i32 + unbox x (fun (k,v) => size v)) : i32

inl rec pickle x (i',s as state) : () =
    match x with
    | () => ()
    | a,b => pickle a state . pickle b state
    | {} => record_iter (fun {key value} => pickle value state) x
    | _ =>
        typecase `x with
        | a _ ~t => 
            pickle (length `a `i32 `t x) state
            am.iter `a `i32 `t (fun x => pickle x state) x
        | string => pickle ($"!x.ToCharArray()" : a i32 char) state
        | ~t => 
            if prim_is x then
                inl i = *i'
                inl length = size_prim `t
                inl s = span.create' `u8 s {i length}
                assert ($"System.BitConverter.TryWriteBytes(!s,!x)" : bool) "Conversion failed."
                i' <- i+length
            elif symbol_is x then ()
            else inl ~x = x in (join unbox x (fun (k,v) => pickle (union_tag x) state . pickle v state)) : ()

inl rec unpickle forall t. (i', s as state) : t =
    inl prim read =
        inl i = *i'
        i' <- i + size_prim `t
        read i
    typecase t with
    | () => ()
    | ~a * ~b => unpickle `a state, unpickle `b state
    | {} => record_type_map (fun k => forall v. => unpickle `v state) `t
    | i32 => prim (fun i => $"System.BitConverter.ToInt32(!s,!i)" : t)
    | char => prim (fun i => $"System.BitConverter.ToChar(!s,!i)" : t)
    | a _ ~y => am.init `a `i32 `y (unpickle `i32 state) (fun i => unpickle `y state)
    | string => inl ar = unpickle `(a i32 char) state in $"System.String(!ar)" : string
    | _ =>
        if symbol_type_is `t then type_to_symbol `t
        else 
            (join
                inl tag = unpickle `i32 state
                union_untag `t tag 
                    (fun k => forall v. => nominal_create `t (k,unpickle `v state)) 
                    (fun _ => failwith `t "Invalid tag.")
                ) : t
