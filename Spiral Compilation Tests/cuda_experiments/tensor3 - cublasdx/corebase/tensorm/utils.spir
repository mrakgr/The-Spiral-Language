open real_core

inl infer_templ forall el. g =
    inl rec f forall el. =
        typecase el with
        | ~a * ~b => f `a, f `b
        | {} => record_type_map (fun k => f) `el
        | _ => g `el
    f `el

// Infers the folded out type of an inferse array. For example, it would map the type `array (a * b)` to `array a * array b`.
// Also supports the same for records.
inl infer forall ar el. = infer_templ `el (forall el. => ``(ar el))

inl map index =
    inl rec f = function
        | a, b => f a, f b
        | {} as ar => record_map (fun {value} => f value) ar
        | ar => index ar
    f

inl iter touch =
    inl rec f = function
        | a, b => f a . f b
        | {} as ar => record_iter (fun {value} => f value) ar
        | ar => touch ar
    f

inl fold g =
    inl rec f s x =
        match x with
        | a, b => f (f s a) b
        | {} as ar => record_fold (fun x => f x.state x.value) s ar
        | ar => g s ar
    f

inl foldBack g =
    inl rec f x s =
        match x with
        | a, b => f a (f b s)
        | {} as ar => record_fold_back (fun x => f x.value x.state) ar s
        | ar => g ar s
    f

inl foldBack2 g =
    inl rec f ar v s =
        match ar, v with
        | (a, b), (va, vb) => f a va (f b vb s)
        | {} & ar, {} & v => record_fold_back (fun x => f x.value (v x.key) x.state) ar s
        | ar, v => g ar v s
    f

inl mapFoldBack g =
    inl rec f x s =
        match x with
        | a, b => 
            inl b,s = f b s
            inl a,s = f a s
            (a, b), s
        | {} as ar => 
            record_fold_back (fun {value key state=m,state} => 
                inl value, state = f value state
                {m with $key=value}, state
                ) ar ({}, s)
        | ar => g ar s
    f

inl iter2 set ar v =
    inl rec f = function
        | (a, b), (va,vb) => f (a, va) . f (b, vb)
        | {} & ar, {} & v => record_iter (fun {key value} => f (ar key, value)) v
        | ar,v => set ar v
    f (ar, v)

inl iter3 set ar v i =
    inl rec f = function
        | (a, b), (va,vb), (ia,ib) => f (a, va, ia) . f (b, vb, ib)
        | {} & ar, {} & v, {} & i => record_iter (fun {key value} => f (ar key, value, i key)) v
        | ar,v,i => set ar v i
    f (ar, v, i)

inl map2 set ar v =
    inl rec f = function
        | (a, b), (va,vb) => f (a, va), f (b, vb)
        | {} & ar, {} & v => record_map (fun {key value} => f (ar key, value)) v
        | ar,v => set ar v
    f (ar, v)

inl assert_index i s = // This could be made concise, but I want the generated code to come out clean.
    if lit_is i && lit_is s then
        if not (0 <= i && i < s ) then
            error_type "Tensor index out of bounds (detected at compile-time.)"
    else
        backend_switch {
            python = fun () => $"assert 0 <= !i < !s, 'Tensor range check'" : ()
            cuda = fun () => $'assert("Tensor range check" && 0 <= !i && !i < !s)' : ()
        }

inl assert_index_view from nearTo dim =
    if lit_is from && lit_is nearTo && lit_is dim then
        if not (0 <= from && from < nearTo && from <= dim) then
            error_type "Tensor index out of bounds (detected at compile-time.)"
    else
        backend_switch {
            python = fun () => $"assert 0 <= !from < !nearTo <= !dim, 'Tensor view range check'" : ()
            cuda = fun () => $'assert("Tensor view range check" && 0 <= !from && !from < !nearTo && !from <= !dim)' : ()
        }

inl assert_indices i = iter2 assert_index i
inl assert_indices_view i = iter3 assert_index_view i
inl calculate_offset a = foldBack2 (fun a b s => a * b + s) a

inl calculate_stride_len dim = mapFoldBack (fun x s => s, x * s) dim 1

inl last dim = fold (fun s x => x) () dim
inl prod dim = foldBack (*) dim 1

// Returns the last element of the stride.
inl assert_stride_contiguous dim stride =
    inl init = last stride
    inl _ = 
        foldBack2 (fun dim stride expected_stride => 
            assert (expected_stride = stride) "The tensor must be contiguous in order to be flattened."
            dim * stride
            ) dim stride init
    init
